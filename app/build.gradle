apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "openfoodfacts.github.scrachx.openfood"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 37
        versionName "0.7.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    flavorDimensions "versionCode"
    productFlavors {
        off {
            applicationId "openfoodfacts.github.scrachx.openfood"
            resValue "string", "app_name", "Open Food Facts"
            buildConfigField 'String', 'HOST', '"https://ssl-api.openfoodfacts.org"'
            buildConfigField 'String', 'OFOTHERLINKAPP', '"openfoodfacts.github.scrachx.openbeauty"'
            buildConfigField 'String', 'OFWEBSITE', '"https://world.openfoodfacts.org/"'
        }

        obf {
            applicationId "openfoodfacts.github.scrachx.openbeauty"
            resValue "string", "app_name", "Open Beauty Facts"
            buildConfigField 'String', 'HOST', '"https://ssl-api.openbeautyfacts.org"'
            buildConfigField 'String', 'OFOTHERLINKAPP', '"openfoodfacts.github.scrachx.openfood"'
            buildConfigField 'String', 'OFWEBSITE', '"https://world.openbeautyfacts.org/"'
        }

        opff {
            applicationId "org.openpetfoodfacts.scanner"
            resValue "string", "app_name", "Open Pet Food Facts"
            buildConfigField 'String', 'HOST', '"https://ssl-api.openpetfoodfacts.org"'
            buildConfigField 'String', 'OFOTHERLINKAPP', '"org.openfoodfacts.scanner"'
            buildConfigField 'String', 'OFWEBSITE', '"https://world.openpetfoodfacts.org/"'
        }
    }

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        multiDexEnabled true
       // jackOptions.enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    packagingOptions{
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    testOptions {
        // avoid "Method ... not mocked."
        unitTests.returnDefaultValues = true
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

apply plugin: 'org.greenrobot.greendao'

greendao {
    schemaVersion 2
}

dependencies {

    //Android
    compile "com.android.support:customtabs:$rootProject.supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:preference-v14:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"

    compile "com.android.support:multidex:$rootProject.multidexVersion"

    //Networking
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-jackson:$rootProject.retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion"

    //Serialization/Deserialization
    compile "com.fasterxml.jackson.core:jackson-core:$rootProject.jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$rootProject.jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-annotations:$rootProject.jacksonVersion"

    //CSV Serialization/Deserialization
    compile "com.opencsv:opencsv:$rootProject.openCsvVersion"

    //Database
    compile "org.greenrobot:greendao:$rootProject.greenDaoVersion"

    //Image Loading
    compile "com.squareup.picasso:picasso:$rootProject.picassoVersion"

    //Image from gallery or camera
    compile "com.github.jkwiecien:EasyImage:$rootProject.easyImageVersion"

    //Binding
    compile "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"

    //Barcode and QR Scanner
    compile "me.dm7.barcodescanner:zxing:$rootProject.qrScannerVersion"

    //HTML Dom
    compile "org.jsoup:jsoup:$rootProject.htmlDomVersion"

    //Apache
    compile "org.apache.commons:commons-collections4:$rootProject.apacheCollectionsVersion"
    compile "commons-validator:commons-validator:$rootProject.apacheValidatorVersion"

    //UI Component : Custom Toast
    compile "net.steamcrafted:load-toast:$rootProject.loadToastVersion"

    //UI Component : ImageView with Zooming
    compile "com.github.chrisbanes:PhotoView:$rootProject.zoomingImgVersion"

    //UI Component : Material Dialog
    compile("com.afollestad.material-dialogs:core:$rootProject.materialDialogVersion") {
        transitive = true
    }

    //UI Component : Material Drawer
    compile('com.mikepenz:materialdrawer:6.0.2@aar') {
        transitive = true
    }

    //UI Component : Adapters
    //DO NOT UPDATE : RecyclerViewCacheUtil removed, needs rework
    compile 'com.mikepenz:fastadapter-commons:2.1.0@aar'

    //UI Component : Fab
    compile "com.github.clans:fab:$rootProject.fabVersion"

    //UI Component : Font Icons
    compile "com.mikepenz:iconics-core:$rootProject.fontIconCoreVersion"
    compile "com.mikepenz:google-material-typeface:$rootProject.googleMaterialFontVersion"
    compile "com.mikepenz:material-design-iconic-typeface:$rootProject.materialFontVersion"
    compile "com.mikepenz:fontawesome-typeface:$rootProject.awesomeFontVersion"

    //Testing
    testCompile "junit:junit:$rootProject.jUnitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testCompile "net.javacrumbs.json-unit:json-unit-fluent:$rootProject.jsonUnitVersion"

    // Required for instrumented tests
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:3.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-web:3.0.1'
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }
}
